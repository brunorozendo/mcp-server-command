plugins {
    id 'java'
    id 'application'
    id 'groovy'
    id 'jacoco' // For code coverage
    id 'org.graalvm.buildtools.native' version '0.10.6'
    id 'org.sonarqube' version '6.2.0.5505'
}

group = 'com.brunorozendo'
version = '0.5.0'

java {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.modelcontextprotocol.sdk:mcp:0.10.0'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.18'
    implementation 'ch.qos.logback:logback-core:1.5.18'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.19.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.19.1'

    // Spock and Groovy for testing
    testImplementation platform('org.spockframework:spock-bom:2.4-M6-groovy-4.0')
    testImplementation 'org.spockframework:spock-core'
    testImplementation 'org.objenesis:objenesis:3.3'
    testImplementation 'net.bytebuddy:byte-buddy:1.14.11'
    testImplementation 'cglib:cglib-nodep:3.3.0'

    // Spock reports
    testImplementation "com.athaydes:spock-reports:2.5.1-groovy-4.0"
}

application {
    mainClass = 'com.brunorozendo.mcp.server.command.McpCommandServer'
}

// Create a fat jar for easier distribution
jar {
    manifest {
        attributes(
            'Main-Class': 'com.brunorozendo.mcp.server.command.McpCommandServer'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // Exclude signature files to avoid security exceptions
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}


test{
    useJUnitPlatform()
    testLogging {
        //events "FAILED","PASSED","SKIPPED","STANDARD_ERROR","STANDARD_OUT","STARTED"
        events "FAILED","PASSED","SKIPPED"
    }
    finalizedBy jacocoTestReport // Generate report after tests run
    maxParallelForks = 20
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                // Add any exclusions here if needed
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.90 // 100% coverage required
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification


graalvmNative {
    toolchainDetection = true
    binaries {
        main {
            imageName = 'mcp-server-command'
            mainClass = application.mainClass.get()
            buildArgs.add('--no-fallback')
            buildArgs.add('-H:+ReportExceptionStackTraces')
            buildArgs.add('--initialize-at-build-time=org.slf4j')
            buildArgs.add('--initialize-at-build-time=ch.qos.logback')
            buildArgs.add('--enable-https')
            buildArgs.add('--enable-all-security-services')
            // Configuration files will be picked up automatically from META-INF/native-image
        }
    }
}

sonarqube {
    properties {
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.layout.buildDirectory.get().asFile}/reports/jacoco/test/*.xml"
        property "sonar.java.coveragePlugin", "jacoco"
    }
}
